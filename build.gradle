plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.9.9"
}

ext {
    buildNumber = System.getenv("ORIGINAL_BUILD_NUMBER") ?: System.getenv("BUILD_NUMBER")
}

group 'com.timgroup'
if (buildNumber) version '1.0.' + buildNumber

repositories {
    maven {
        name "nexus"
        url "${repoUrl}/groups/public"
    }
}

gradlePlugin {
    plugins {
        webpack {
            id = "com.timgroup.webpack"
            implementationClass = "com.timgroup.gradle.webpack.WebpackPlugin"
        }
    }
}

pluginBundle {
    website = "http://www.timgroup.com/"
    vcsUrl = "https://github.com/tim-group/gradle-webpack-plugin"
    description = "Build Javascript sources with Webpack and test with Mocha"
    plugins {
        webpack {
            id = "com.timgroup.webpack"
            displayName = "Webpack / Mocha plugin"
            tags = ["webpack", "mocha", "nodejs"]
        }
    }
}

publishing {
    repositories {
        maven {
            name = "timgroup"
            url = "${project.repoUrl}/repositories/yd-release-candidates"
            credentials {
                username = project.repoUsername
                password = project.repoPassword
            }
        }
    }
}

model {
    publishing {
        publications {
            pluginMaven {
                artifact sourcesJar
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile "junit:junit:4.12"
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}
